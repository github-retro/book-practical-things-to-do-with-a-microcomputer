10 PRINT "AVERAGES PROGRAM"
20 DIM X(50)
30 LET N=0
40 PRINT "YOU CAN TYPE IN UP TO"
50 PRINT "50 NUMBERS"
60 PRINT
70 PRINT "TYPE 999 AT THE END OF"
80 PRINT "YOUR NUMBER LIST"
90 PRINT
100 LET N=N+1
110 IF N>50 THEN GOTO 160
120 PRINT "TYPE IN NUMBER ";N
130 INPUT X(N)
140 IF X(N)=999 THEN GOTO 160
150 GOTO 100
160 LET N=N-1
170 PRINT "YOU HAVE ENTERED ";N;" NUMBERS"
180 PRINT
190 LET T=0
200 FOR I=1 TO N
210 LET T=T+X(I)
220 NEXT I
230 PRINT "THE TOTAL IS ";T
240 LET A=T/N
250 PRINT "THE AVERAGE IS ";A
260 LET B=X(1)
270 LET S=X(1)
280 FOR I=2 TO N
290 IF X(I)>B THEN LET B=X(I)
300 IF X(I)<S THEN LET S=X(I)
310 NEXT I
320 PRINT "BIGGEST NUMBER IS ";B
330 PRINT "SMALLEST NUMBER IS ";S
340 PRINT
350 GOTO 30