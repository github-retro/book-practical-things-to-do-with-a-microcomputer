10 PRINT "HOROSCOPE GENERATOR"
20 DATA 0,31,59,90,120,151,181,212,243,273,304,334
30 DIM M(12)
40 FOR I=1 TO 12: READ M(I): NEXT I
50 DATA "AQUARIUS",20,"PISCES",50,"ARIES",80
60 DATA "TAURUS",111,"GEMINI",141,"CANCER",172
70 DATA "LEO",203,"VIRGO",234,"LIBRA",265,
80 DATA "SCORPIO",296,"SAGITTARIUS",326
90 DATA "CAPRICORN",355
100 DATA "YOU WILL MEET A DARK, MYSTERIOUS STRANGER"
110 DATA "YOU WILL BE RICH AND HAPPY WRITING PROGRAMS"
120 DATA "YOU WILL LEARN THE MEANING OF LIFE"
130 DATA "YOU WILL BE VERY HAPPY LIVING IN HOLLYWOOD"
140 DATA "YOU WILL FALL IN LOVE WITH A GREEN COMPUTER"
150 DATA "YOU WOULD BE HAPPIER A LONG WAY AWAY"
160 DATA "LEARN TO LOVE FROGS AND TOADS"
170 DATA "DO NOT RUN FOR BUSES IN THE RAIN"
180 DATA "YOUR CHILDREN WILL NOT BELIEVE IN HOROSCOPES"
190 DATA "ALWAYS TELL THE TRUTH ON THURSDAYS"
200 DATA "YOUR FACE WILL BE YOUR FOTUNE - SOMEHOW"
210 DATA "TAKE NO NOTICE OF ANY HOROSCOPE"
220 DIM S$(12),S(12)
230 FOR I=1 TO 12
240 READ S$(I),S(I)
250 NEXT I
260 DIM D$(12)
270 FOR I=1 TO 12
280 READ D$(I)
290 NEXT I
300 PRINT: PRINT: PRINT
310 PRINT "PLEASE TYPE THE NUMBER OF THE MONTH"
320 PRINT "IN WHICH YOU WERE BORN"
330 PRINT "(JAN=1, FEB=2, ETC)"
340 INPUT M
350 IF M<1 OR M>12 THEN PRINT "NO SUCH MONTH": GOTO 300
360 PRINT
370 PRINT "PLEASE TYPE IN THE DAY OF THE MONTH"
380 INPUT D
390 IF D<1 OR D>31 THEN PRINT "NO SUCH DAY IN ANY MONTH": GOTO 360
400 LET X=M(M)+D
410 LET A=0
420 FOR I=1 TO 11
430 IF (X>=S(I)) AND (X<S(I+1)) THEN LET A=I
440 NEXT I
450 IF A=0 THEN LET A=12
460 CLS
470 PRINT: PRINT: PRINT
480 PRINT "YOUR STAR SIGN IS: ";S$(A)
490 PRINT
500 PRINT D$(A)
510 PRINT: PRINT
520 GOTO 300
